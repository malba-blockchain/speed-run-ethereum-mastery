// SPDX-License-Identifier: MIT

pragma solidity >=0.8.0 <0.9.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

/**
 * @title DEX Template
 * @author stevepham.eth and m00npapi.eth
 * @notice Empty DEX.sol that just outlines what features could be part of the challenge (up to you!)
 * @dev We want to create an automatic market where our contract will hold reserves of both ETH and ðŸŽˆ Balloons. These reserves will provide liquidity that allows anyone to swap between the assets.
 * NOTE: functions outlined here are what work with the front end of this challenge. Also return variable names need to be specified exactly may be referenced (It may be helpful to cross reference with front-end code function calls).
 */
contract DEX {
    /* ========== GLOBAL VARIABLES ========== */

    IERC20 token; //instantiates the imported contract

    uint256 public totalLiquidity;

    mapping(address => uint256) public liquidity;

    /* ========== EVENTS ========== */

    /**
     * @notice Emitted when ethToToken() swap transacted
     */
    event EthToTokenSwap(address swapper, uint256 tokenOutput, uint256 ethInput);

    /**
     * @notice Emitted when tokenToEth() swap transacted
     */
    event TokenToEthSwap(address swapper, uint256 tokensInput, uint256 ethOutput);

    /**
     * @notice Emitted when liquidity provided to DEX and mints LPTs.
     */
    event LiquidityProvided(address liquidityProvider, uint256 tokensInput, uint256 ethInput, uint256 liquidityMinted);

    /**
     * @notice Emitted when liquidity removed from DEX and decreases LPT count within DEX.
     */
    event LiquidityRemoved(
        address liquidityRemover,
        uint256 tokensOutput,
        uint256 ethOutput,
        uint256 liquidityWithdrawn
    );

    /* ========== CONSTRUCTOR ========== */

    constructor(address token_addr) public {
        token = IERC20(token_addr); //specifies the token address that will hook into the interface and be used through the variable 'token'
    }

    /* ========== MUTATIVE FUNCTIONS ========== */

    /**
     * @notice initializes amount of tokens that will be transferred to the DEX itself from the erc20 contract mintee (and only them based on how Balloons.sol is written). Loads contract up with both ETH and Balloons.
     * @param tokens amount to be transferred to DEX
     * @return totalLiquidity is the number of LPTs minting as a result of deposits made to DEX contract
     * NOTE: since ratio is 1:1, this is fine to initialize the totalLiquidity (wrt to balloons) as equal to eth balance of contract.
     */
    function init(uint256 tokens) public payable returns (uint256) {

        require(totalLiquidity == 0, "DEX: init - already has liquidity");
        totalLiquidity = address(this).balance;
        liquidity[msg.sender] = totalLiquidity;
        require(token.transferFrom(msg.sender, address(this), tokens), "DEX: init - transfer did not transact");
        return totalLiquidity;
    }

    /**
     * @notice returns yOutput, or yDelta for xInput (or xDelta)
     * @dev Follow along with the [original tutorial](https://medium.com/@austin_48503/%EF%B8%8F-minimum-viable-exchange-d84f30bd0c90) Price section for an understanding of the DEX's pricing model and for a price function to add to your contract. You may need to update the Solidity syntax (e.g. use + instead of .add, * instead of .mul, etc). Deploy when you are done.
     */
    function price( uint256 xInput, uint256 xReserves, uint256 yReserves ) public pure returns (uint256 yOutput) {

        //This price is made considering a 3% exchange fees

        uint256 xInputWithFee = xInput*997;
        uint256 numerator = xInputWithFee*yReserves;
        uint256 denominator = (xReserves*1000) + xInputWithFee;
         
        yOutput = numerator / denominator;

        return yOutput;
    }

    /**
     * @notice returns liquidity for a user.
     * NOTE: this is not needed typically due to the `liquidity()` mapping variable being public and having a getter as a result. This is left though as it is used within the front end code (App.jsx).
     * NOTE: if you are using a mapping liquidity, then you can use `return liquidity[lp]` to get the liquidity for a user.
     * NOTE: if you will be submitting the challenge make sure to implement this function as it is used in the tests.
     */
    function getLiquidity(address lp) public view returns (uint256) {

        return liquidity[lp];
    }

    /**
     * @notice sends Ether to DEX in exchange for $BAL
     */
    function ethToToken() public payable returns (uint256 tokenOutput) {
        
        require(msg.value > 0, "cannot swap 0 ETH");

        uint256 xInput = msg.value;
        uint256 xReserves = address(this).balance - msg.value;
        uint256 yReserves = token.balanceOf(address(this));

        tokenOutput = price(xInput, xReserves, yReserves);

        require(token.transfer(msg.sender, tokenOutput), "ethToToken(): Reverte swap.");
        emit EthToTokenSwap(msg.sender, tokenOutput, msg.value);

        return tokenOutput;
    }

    /**
     * @notice sends $BAL tokens to DEX in exchange for Ether
     */
    function tokenToEth(uint256 tokenInput) public returns (uint256 ethOutput) {

        require(tokenInput > 0, "cannot swap 0 tokens");

        uint256 xInput = tokenInput;
        uint256 xReserves = token.balanceOf(address(this));
        uint256 yReserves = address(this).balance;

        ethOutput = price(xInput, xReserves, yReserves);

        require(token.transferFrom(msg.sender, address(this), tokenInput), "tokenToEth(): reverted swap.");

        (bool sent,) = msg.sender.call{value: ethOutput}("");
        require(sent, "tokenToEth: revert in transferring eth to you!");

        emit TokenToEthSwap(msg.sender, tokenInput, ethOutput);
        return ethOutput;
    }

    /**
     * @notice allows deposits of $BAL and $ETH to liquidity pool
     * NOTE: parameter is the msg.value sent with this function call. That amount is used to determine the amount of $BAL needed as well and taken from the depositor.
     * NOTE: user has to make sure to give DEX approval to spend their tokens on their behalf by calling approve function prior to this function call.
     * NOTE: Equal parts of both assets will be removed from the user's wallet with respect to the price outlined by the AMM.
     */
    function deposit() public payable returns (uint256 tokenDeposit) {

        // Check if the sender has sent some ether (ETH) value along with the transaction
        require(msg.value > 0, "Must send value when depositing");

        // Calculate the current ETH balance of the contract minus the value sent in this transaction
        uint256 ethReserve = address(this).balance - msg.value;

        // Get the current token balance held by the contract
        uint256 tokenReserve = token.balanceOf(address(this));

        // Calculate the amount of tokens to deposit based on the ETH value sent, token reserve, and ETH reserve
        // Note: Adding 1 to ensure rounding up (instead of rounding down)
        //tokenDeposit = (msg.value * tokenReserve / ethReserve) + 1;
        tokenDeposit = (msg.value * tokenReserve / ethReserve);

        // Calculate the amount of liquidity tokens to mint for the sender based on their ETH deposit
        uint256 liquidityMinted = msg.value * totalLiquidity / ethReserve;

        // Transfer tokens from the sender to this contract, representing their deposit
        // If this transfer fails, it will revert the transaction with the specified error message
        require(token.transferFrom(msg.sender, address(this), tokenDeposit), "Error on transferring the tokens to the DEX");

        // Increase the liquidity balance of the sender
        liquidity[msg.sender] += liquidityMinted;

        // Increase the total liquidity supply
        totalLiquidity += liquidityMinted;

        emit LiquidityProvided(msg.sender, tokenDeposit, msg.value, liquidityMinted);

        // Return the amount of tokens deposited
        return tokenDeposit;

    }

    /**
     * @notice allows withdrawal of $BAL and $ETH from liquidity pool
     * NOTE: with this current code, the msg caller could end up getting very little back if the liquidity is super low in the pool. I guess they could see that with the UI.
     */
    function withdraw(uint256 amount) public returns (uint256 eth_amount, uint256 token_amount) {
    // Ensure that the sender has enough liquidity tokens to withdraw the specified 'amount'
        require(liquidity[msg.sender] >= amount, "withdraw: sender does not have enough liquidity to withdraw");

        // Get the current ETH reserve held by the contract
        uint256 ethReserve = address(this).balance;

        // Get the current token reserve held by the contract
        uint256 tokenReserve = token.balanceOf(address(this));

        // Calculate the amount of ETH to withdraw based on the 'amount' and total liquidity in the pool
        uint256 ethWithdrawn = amount * ethReserve / totalLiquidity;

        // Calculate the amount of tokens to withdraw based on the 'amount' and total liquidity in the pool
        uint256 tokenAmount = amount * tokenReserve / totalLiquidity;

        // Deduct the 'amount' from the sender's liquidity balance
        liquidity[msg.sender] -= amount;

        // Deduct the 'amount' from the total liquidity supply
        totalLiquidity -= amount;

        // Attempt to transfer ETH to the sender
        (bool sent,) = payable(msg.sender).call{value: ethWithdrawn}("");

        // Ensure that the ETH transfer was successful, or revert with an error message
        require(sent, "withdraw(): revert in transferring eth to you!");

        // Transfer tokens to the sender
        require(token.transfer(msg.sender, tokenAmount), "withdraw(): revert in transferring tokens to you!");

        // Emit an event to log the liquidity removal
        emit LiquidityRemoved(msg.sender, tokenAmount, ethWithdrawn, amount);

        // Return the amounts of ETH and tokens withdrawn
        return (ethWithdrawn, tokenAmount);
    }
}

